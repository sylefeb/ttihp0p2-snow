// SL 2024-08-27 @sylefeb
// ========================================================
// MIT license
// ========================================================

// include VGA signal generator
$include('../common/vga.si')

// -------------------------

$$ W = 640
$$ H = 480

unit vga_demo(
  output! uint2 video_r,
  output! uint2 video_g,
  output! uint2 video_b,
  output  uint1 video_hs,
  output  uint1 video_vs,
) {

  vga vga(
    vga_hs :> video_hs,
    vga_vs :> video_vs,
  );

  uint12 frame(0); // frame counter
  uint1  prev_vs(0);
$$for i=0,2 do
  uint12  rand_$i$(0);
$$end

  always {

    // shading pipeline
    {
      // pipeline start

      uint10  x = vga.vga_x;
      uint10  y = vga.vga_y;
      uint2   s = 0;

$$for i=0,1 do
    ->
      rand_$i$ = (x == (frame>>$i$))
               ? 12d$1+i*103$
               : (rand_$i$ * 12d$4091$ + 12d$3331+i*313$);
      uint2  speed  = rand_$i$[10,2];
    //->
      uint10 dotpos = (frame[0,10] >> speed) + rand_$i$;
    //->
      uint2 l = (dotpos[0,8] == y[0,8])
              ? (speed == 2b11 ? 2b01 : ~speed)
              : 0;
      s = s | l;
$$end
      video_r = vga.active ? s : 2b0;
      video_g = vga.active ? s : 2b0;
      video_b = vga.active ? s : 2b0;

    }

    // count frames
    uint1 frame_tick  = (prev_vs & ~vga.vga_vs); // start of vsynch
    frame    = frame_tick ? (frame + 1) : frame;
    prev_vs  = vga.vga_vs;

    if (frame_tick) {
      __display("frame %d",frame);
    }

  }

}

// -------------------------
